# $Id: configure.in,v 1.37 2006/12/19 04:18:07 pgeyleg Exp $

AC_INIT(MIME Directory Library, 0.4.0, mailto:srittau@jroger.in-berlin.de, libmimedir)
AC_PREREQ(2.53)
AC_CONFIG_SRCDIR(mimedir/mimedir-attribute.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AC_PROG_INTLTOOL
AM_CONFIG_HEADER(config.h)

MIMEDIR_MAJOR_VERSION=0
MIMEDIR_MINOR_VERSION=4
MIMEDIR_MICRO_VERSION=0
AC_SUBST(MIMEDIR_MAJOR_VERSION)
AC_SUBST(MIMEDIR_MINOR_VERSION)
AC_SUBST(MIMEDIR_MICRO_VERSION)
MIMEDIR_VERSION=$MIMEDIR_MAJOR_VERSION.$MIMEDIR_MINOR_VERSION.$MIMEDIR_MICRO_VERSION

LT_CURRENT=0
LT_REVISION=0
LT_AGE=0
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AM_PROG_LIBTOOL

AC_STDC_HEADERS
AC_PROG_INSTALL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_STRUCT_TIMEZONE

dnl Check for Gnome Packages
PKG_CHECK_MODULES(GLIB, glib-2.0 >= 2.0.0 gobject-2.0 >= 2.0.0)
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)

GLIB_GENMARSHAL="`$PKG_CONFIG --variable=glib_genmarshal glib-2.0`"
AC_SUBST(GLIB_GENMARSHAL)

dnl gettext Related Stuff
GETTEXT_PACKAGE=libmimedir
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name to be used by gettext.])
ALL_LINGUAS="ar az ca cs de dz el en_CA en_GB es fi fr ga hr ml ne nl pl pt pt_BR rw sr sr@Latn sv uk vi zh_CN"
AM_GLIB_GNU_GETTEXT

##################################################
# Check for gtk-doc
##################################################

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6
if $GTKDOC ; then 
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])
    if perl <<EOF ; then
      exit (("$gtk_doc_version" =~ /^[[0-9]]+\.[[0-9]]+$/) &&
            ("$gtk_doc_version" >= "$gtk_doc_min_version") ? 0 : 1);
EOF
      AC_MSG_RESULT(yes)
   else
      AC_MSG_RESULT(no)
      GTKDOC=false
   fi
fi

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)

##################################################
# Output
##################################################

dnl Final Output
AC_CONFIG_FILES([
	Makefile
	po/Makefile.in
	mimedir/Makefile
	bin/Makefile
	docs/Makefile
	docs/reference/Makefile
	docs/reference/mimedir/Makefile
	mimedir-1.0.pc])
AC_OUTPUT
